# Copyright 2014 George King.
# Permission to use this file is granted in ploy/license.txt.

# core macros and functions loaded before library or user code.
# the struct syntax {...} is used to construct special form code vectors directly,
# before the idiomatic macros are defined.
# once available, the macros should be used instead of the raw forms,
# as they provide a layer of abstraction between code and the exact evaluation semantics,
# as well as increased expressiveness and syntactic consistency.

{LET errRL
  # print a representation to std-err.
  # temporary definition for debugging.
  {FN
    errRL
    FALSE
    {-obj}
    {DO
      (raw_write_repr (el std-err 1) obj)
      (raw_write (el std-err 1) '\n')}}}

{LET macro
  # the 'macro' macro defines a new macro in the current environment.
  # TODO: refine environment semantics?
  # note: if core was already available, we would define 'macro' using itself like so:
  ##<macro macro {-sym -pars &body}
      <let sym
        [ FN # this form creates a new function/macro that...
          sym # contains its name/symbol,
          TRUE # is a macro,
          pars # has the specified parameter list,
          <if (eq (len body) 1) # and body...
            (el body 0)
            (prepend DO body)>]>> # wrapped in a do form if necessary.
  # instead, we must define it using raw forms.
  # note the structural similarity between the outer FN form and the inner vec literal.
  {FN # creates both functions and macros
    macro # the symbol for this macro is `macro;
    # we do not quote it because this is a raw form, so it is not evaluated.
    TRUE # is-macro
    {-sym -pars &body} # the parameter list; also unquoted, as parameter lists always are.
    [LET # the body of the 'macro' macro is a LET form.
      sym # the symbol parameter that the macro to be created is bound to (not `macro).
      [FN # the value of the binding is created by evaluating the FN form;
        # note that this inner FN is inside a vec literal;
        # when the 'macro' macro is expanded, the literal is evaluated,
        # and a {FN ...} form is the resulting code vector.
        sym
        TRUE
        pars
        {IF (eq (len body) 1)
          (el body 0)
          (prepend DO body)}]]}}

<macro fn {-sym -pars &body}
  # defines a function in the current environment.
  # TODO: refine environment semantics?
  # this is conceptually the same as macro; see the explanation above.
  [LET sym
    [ FN
      sym
      FALSE
      pars
      {IF (eq (len body) 1)
        (el body 0)
        (prepend DO body)}]]>

# define macros to wrap the special forms.

<macro quo {-expr}
  # absolute quotation of an expression.
  # evaluating a QUO form simply yields the contained expression, unevaluated.
  [QUO expr]>

<macro qua {-expr}
  # quasiquotation of an expression.
  # this is similar to quo, except that subexpressions can be unquoted,
  # in which case they are evaluated.
  # QUA forms themselves do not evaluate; they are converted by the preprocessor.
  # equivalent to `expr syntax.
  # TODO: implement quasiquotation as a macro instead of in the preprocessor?
  [QUA expr]>

<macro unq {-expr}
  # unquotation of an expression.
  # this form indicates that the unquoted subexpression should be evaluated within a qua form.
  # this for does not evaluate, and therefore must be inside of a qua,
  # where it will get transformed appropriately.
  # equivalent to ,expr syntax
  [UNQ expr]>

<macro do {&exprs}
  (prepend DO exprs)>

<macro let {-sym -expr}
  # bind the value of expr to sym in the current environment.
  [LET sym expr]>

<macro if-then {-pred -then}
  # 'if' variant that takes exactly two expressions; useful for bootstrapping the 'if' macro.
  [IF pred then ``VOID]>

<macro if-then-else {-pred -then -else}
  # 'if' variant that takes exactly three expressions; useful for bootstrapping the 'if' macro.
  [IF pred then else]>

<fn error {-msg}
  # raise an error. for now we can only print the message and exit.
  (raw_write (el std-err 1) 'error: ')
  (raw_write (el std-err 1) msg)
  (raw_write (el std-err 1) '\n')
  (exit 1)>

<macro if {&exprs}
  # generic 'if'. inspired by paul graham's arc-lisp.
  <if-then (lt (len exprs) 2) (error "degenerate if form")>
  <fn f {-exprs}
    <let l (len exprs)>
    <let h (hd exprs)>
    (errRL l)
    (errRL h)
    (errRL exprs)
    <if-then-else (eq l 1)
      h # a lone expr is the final else statement
      <do
        <let then (el exprs 1)>
        <if-then-else (eq l 2)
          [IF h then ``VOID] # final if-else
          [IF h then (f (slice exprs 0 2))]>>>>
  (f exprs)>

<macro when {-pred &exprs}
  # if 'pred', do 'exprs'. else none.
  [IF pred [DO exprs] `VOID]>

